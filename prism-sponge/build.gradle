import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id 'prism.java-conventions'
    id 'org.spongepowered.gradle.plugin' version '2.0.2'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

dependencies {
    compileOnly project.property('deps.sponge')
    implementation project.property('deps.guice')
    implementation project.property('deps.mysql')
    implementation project.property('deps.slf4j')
    implementation project(':prism-core')
    implementation project(':prism-api')
}

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17

sponge {
    apiVersion('8.0.0')
    license('GPL')
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version('1.0')
    }
    plugin('prism') {
        displayName('Prism')
        entrypoint('network.darkhelmet.prism.PrismSponge')
        description(project.property('description') as String)
        links {
            source(project.property('website') as String)
            issues(project.property('issues') as String)
        }
        contributor('viveleroi') {
            description('Creator')
        }
        dependency('spongeapi') {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}