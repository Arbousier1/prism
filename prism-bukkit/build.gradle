plugins {
    id 'prism.java-conventions'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'xyz.jpenilla.run-paper' version '1.0.6'
    id 'net.minecrell.plugin-yml.bukkit' version '0.5.2'
}

dependencies {
    compileOnly project.property('deps.spigot-api')
    implementation project.property('deps.adventure-api')
    implementation project.property('deps.adventure-bukkit')
    implementation project.property('deps.adventure-minimessage')
    implementation project.property('deps.adventure-plain')
    implementation project.property('deps.apache-commons-collections')
    implementation project.property('deps.configurate-hocon')
    implementation project.property('deps.expiringmap')
    implementation project.property('deps.fastutil')
    implementation project.property('deps.guice')
    implementation project.property('deps.moonshine-core')
    implementation project.property('deps.moonshine-standard')
    implementation project.property('deps.nbt-api')
    implementation project.property('deps.p6spy')
    implementation project.property('deps.slf4j')
    implementation project.property('deps.taskchain')
    implementation project.property('deps.triumph-cmd-bukkit')
    implementation project(':prism-core')
    implementation project(':prism-api')
}

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17

// "parameters" flag required for moonshine to work
compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

bukkit {
    name = 'prism'
    main = 'network.darkhelmet.prism.PrismBukkit'
    apiVersion = '1.17'
    authors = ['viveleroi']
    website = project.property('website')
}

shadowJar {
    relocate 'com.google.inject', 'network.darkhelmet.prism.inject'
    relocate 'co.aikar.taskchain', 'network.darkhelmet.prism.taskchain'
    relocate 'com.zaxxer', 'network.darkhelmet.prism.zaxxer'
    relocate 'net.kyori', 'network.darkhelmet.prism.kyori'
    relocate 'org.spongepowered', 'network.darkhelmet.prism.spongepowered'
    relocate 'de.tr7zw', 'network.darkhelmet.prism.tr7zw'

    minimize {
        exclude(dependency('p6spy:.*:.*'))
    }
}

runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion('1.19')
}