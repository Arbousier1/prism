plugins {
    id 'prism.java-conventions'
    id 'com.gradleup.shadow' version '9.0.0-beta11'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'xyz.jpenilla.resource-factory-bukkit-convention' version '1.2.0'
}

dependencies {
    compileOnly project.property('deps.log4j-api')
    compileOnly project.property('deps.spigot-api')
    implementation project(':prism-loader')
}

shadowJar {
    archiveFileName = "prism-bukkit-${project.ext.fullVersion}.jar"

    from {
        project(':prism-bukkit').tasks.shadowJar.archiveFile
    }

    relocate 'com.fasterxml.jackson', 'network.darkhelmet.prism.libs.jackson'
    relocate 'org.objectweb.asm', 'network.darkhelmet.prism.asm'
    relocate 'org.spongepowered.configurate', 'network.darkhelmet.prism.libs.configurate'
    relocate 'com.typesafe', 'network.darkhelmet.prism.libs.typesafe'
    relocate 'io.leangen.geantyref', 'network.darkhelmet.prism.libs.geantyref'
    relocate 'net.kyori.adventure', 'network.darkhelmet.prism.libs.adventure'
    relocate 'net.kyori.examination', 'network.darkhelmet.prism.libs.examination'
}

artifacts {
    archives shadowJar
}

runServer {
    minecraftVersion("1.21.4")
    legacyPluginLoading()
    serverJar(file("./run-spigot/spigot-1.21.4.jar"))
    jvmArgs("-Dcom.mojang.eula.agree=true")
}

bukkitPluginYaml {
    name.set('prism')
    version.set(project.property('version'))
    main.set('network.darkhelmet.prism.PrismBukkitPluginLoader')
    apiVersion.set(project.property('spigot-api-version'))
    website.set(project.property('website'))
    authors.add("viveleroi")
    load.set(xyz.jpenilla.resourcefactory.bukkit.BukkitPluginYaml.PluginLoadOrder.STARTUP)
}
