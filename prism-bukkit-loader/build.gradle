plugins {
    id 'prism.java-conventions'
    id 'com.gradleup.shadow' version '9.0.0-beta11'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'de.eldoria.plugin-yml.bukkit' version '0.7.1'
}

dependencies {
    compileOnly project.property('deps.log4j-api')
    compileOnly project.property('deps.paper-api')
    implementation project(':prism-loader')
}

bukkit {
    name = 'prism'
    main = 'network.darkhelmet.prism.PrismBukkitPluginLoader'
    apiVersion = '1.21.4'
    authors = ['viveleroi']
    website = project.property('website')
    load = 'STARTUP'
}

shadowJar {
    archiveFileName = "prism-bukkit-${project.ext.fullVersion}.jar"

    from {
        project(':prism-bukkit').tasks.shadowJar.archiveFile
    }

    relocate 'com.fasterxml.jackson', 'network.darkhelmet.prism.libs.jackson'
    relocate 'org.objectweb.asm', 'network.darkhelmet.prism.asm'
    relocate 'org.spongepowered.configurate', 'network.darkhelmet.prism.libs.configurate'
    relocate 'com.typesafe', 'network.darkhelmet.prism.libs.typesafe'
    relocate 'io.leangen.geantyref', 'network.darkhelmet.prism.libs.geantyref'
    relocate 'net.kyori.examination', 'network.darkhelmet.prism.libs.examination'
}

artifacts {
    archives shadowJar
}

runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion('1.21.4')
}